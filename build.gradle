buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "net.ltgt.gradle:gradle-apt-plugin:0.6"
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
  }
}

apply plugin: 'java'
apply plugin: "net.ltgt.apt"

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

repositories {
  jcenter()
  maven {url "https://clojars.org/repo/"}
}

def buildProps = new Properties()
file("local.properties").withInputStream {
  stream -> buildProps.load(stream)
}

dependencies {
  compile 'com.squareup.moshi:moshi:1.1.0'
  apt 'com.google.auto.value:auto-value:1.2-rc1'
  apt 'com.ryanharter.auto.value:auto-value-moshi:0.2.2'
  apt 'frankiesardo:auto-parcel:1.0.1'
  compileOnly files("${buildProps['sdk.dir']}/platforms/android-23/android.jar")
  compileOnly files("${buildProps['sdk.dir']}/extras/android/m2repository/com/android/support/support-annotations/23.4.0/support-annotations-23.4.0.jar")
  compileOnly 'com.google.code.findbugs:jsr305:3.0.0'
  compileOnly 'com.google.auto.value:auto-value:1.2-rc1'
  testCompile 'junit:junit:4.12'
  testCompileOnly 'com.google.code.findbugs:jsr305:3.0.0'
  testCompileOnly 'com.google.auto.value:auto-value:1.2-rc1'
  testApt 'com.google.auto.value:auto-value:1.2-rc1'
  testApt 'com.ryanharter.auto.value:auto-value-moshi:0.2.2'
}

test {
  testLogging {
    events "passed", "skipped", "failed", "standardOut", "standardError"
  }
}

apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group = 'moe.banana.jsonapi'
version = '1.0.15'

task sourceJar (type: Jar, dependsOn: classes) {
  classifier 'sources'
  from sourceSets.main.allJava
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      artifactId 'moshi-jsonapi-android'
      artifact sourceJar
    }
  }
}

bintray {
  user = System.getenv('BINTRAY_USER')
  key = System.getenv('BINTRAY_KEY')
  publications = ['mavenJava']
  dryRun = false
  publish = true
  pkg {
    repo = 'maven'
    name = project.name
    vcsUrl = 'https://github.com/kirisetsz/moshi-jsonapi.git'
    licenses = ['MIT']
    version {
      name = project.version
    }
  }
}
