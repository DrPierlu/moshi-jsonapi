buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:2.0.0'
    classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
    classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
  }
}

group = 'moe.banana'

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.neenbedankt.android-apt'

android {
  compileSdkVersion 23
  buildToolsVersion "23.0.2"

  defaultConfig {
    minSdkVersion 9
    targetSdkVersion 23
    versionCode 1
    versionName "1.0"
    consumerProguardFiles 'proguard-rules.pro'
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }
}

repositories {
  jcenter()
  maven {url "https://clojars.org/repo/"}
}

dependencies {
  compile 'com.squareup.moshi:moshi:1.1.0'
  apt 'com.google.auto.value:auto-value:1.2-rc1'
  apt 'com.ryanharter.auto.value:auto-value-moshi:0.2.2'
  apt 'frankiesardo:auto-parcel:1.0.1'
  compile 'com.android.support:support-annotations:23.4.0'
  compile 'com.google.auto.value:auto-value:1.2-rc1'
}

// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

// build a jar file in addition to the default aar file
android.libraryVariants.all { variant ->
    def name = variant.buildType.name
    def task = project.tasks.create "jar${name.capitalize()}", Jar
    task.dependsOn variant.javaCompile
    task.from variant.javaCompile.destinationDir
    artifacts.add('archives', task);
}

install {
  repositories.mavenInstaller {
    pom.project {
      licenses {
        license {
          name 'The MIT License'
          url 'https://github.com/kamikat/moshi-jsonapi/blob/master/LICENSE'
          distribution 'repo'
        }
      }
    }
  }
}
